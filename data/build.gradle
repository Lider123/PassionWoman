plugins {
    id 'com.android.library'
    id 'jacoco'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
}

apply from: "$project.rootDir/tools/jacoco.gradle"

def fcmPropertiesFile = rootProject.file("data/fcm.properties")
def fcmProperties = new Properties()
fcmProperties.load(new FileInputStream(fcmPropertiesFile))

android {
    namespace "ru.babaetskv.passionwoman.data"

    defaultConfig {
        minSdk 21
        compileSdk 34
        targetSdk 34

        buildConfigField 'String', 'API_URL', '"http://192.168.1.167:8080"' // TODO: check
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions = ["version"]
    productFlavors {
        demo {
            dimension "version"
            versionNameSuffix "-demo"
            buildConfigField("String", "FCM_KEY", "\"${fcmProperties['SERVER_KEY']}\"")
        }
        full {
            dimension "version"
            versionNameSuffix ""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    testOptions {
        unitTests.returnDefaultValues true
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation project(":domain")

    // JSON handling
    def moshi_version = "1.12.0"
    api "com.squareup.moshi:moshi:$moshi_version"
    api "com.squareup.moshi:moshi-kotlin:$moshi_version"

    // Preferences
    def kotpref_version = "2.13.1"
    api "com.chibatching.kotpref:kotpref:$kotpref_version"
    implementation "com.chibatching.kotpref:enum-support:$kotpref_version"

    // Network
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"
    implementation "ru.gildor.coroutines:kotlin-coroutines-okhttp:1.0"

    // Database
    def room_version = "2.5.2"
    demoImplementation "androidx.room:room-ktx:$room_version"
    demoAnnotationProcessor "androidx.room:room-compiler:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // Testing
    testImplementation project(path: ":domain", configuration: "testDependencies")
    androidTestImplementation project(path: ":domain", configuration: "androidTestDependencies")
}