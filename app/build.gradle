plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
}
if (getGradle().getStartParameter().getTaskRequests().toString().contains("hms")) {
    apply plugin: 'com.huawei.agconnect'
}
if (getGradle().getStartParameter().getTaskRequests().toString().contains("gms")) {
    apply plugin: 'com.google.gms.google-services'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "ru.babaetskv.passionwoman.app"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 2
        versionName "0.1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField 'Boolean', 'ANALYTICS_ENABLED', 'false'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField 'Boolean', 'ANALYTICS_ENABLED', 'true'
        }
    }
    flavorDimensions "functionality", "services"
    productFlavors {
        demo {
            dimension "functionality"
            applicationIdSuffix ""
            versionNameSuffix "-demo"
        }
        full {
            dimension "functionality"
            applicationIdSuffix ""
            versionNameSuffix ""
        }
        hms {
            dimension "services"
            applicationIdSuffix ".huawei"
            versionNameSuffix "-hms"
        }
        gms {
            dimension "services"
            applicationIdSuffix ""
            versionNameSuffix "-gms"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "$applicationName${variant.productFlavors[0].versionNameSuffix}-${defaultConfig.versionName}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    implementation project(":data")
    implementation project(":domain")

    // Common
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation "android.arch.lifecycle:extensions:1.1.1"

    // Navigation
    implementation "com.github.terrakok:cicerone:$cicerone_version"

    // DI
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    // ViewBinding
    implementation "com.github.yogacp:android-viewbinding:1.0.3"

    // Shimmer
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    // Circle indicator
    implementation "me.relex:circleindicator:2.1.6"

    // Country code picker
    implementation 'com.hbb20:ccp:2.4.0'

    // Pin entry view
    implementation 'me.philio:pinentryview:1.0.6'

    // Image picker
    implementation 'com.github.dhaval2404:imagepicker:2.1'

    // AdapterDelegates
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl:$adapter_delegates_version"
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl-viewbinding:$adapter_delegates_version"

    // Firebase
    gmsImplementation platform('com.google.firebase:firebase-bom:28.0.1')
    gmsImplementation 'com.google.firebase:firebase-analytics-ktx'
    gmsImplementation 'com.google.firebase:firebase-auth-ktx'

    // Huawei Analytics Kit
    hmsImplementation 'com.huawei.agconnect:agconnect-core:1.3.1.300'
    hmsImplementation 'com.huawei.hms:hianalytics:5.0.0.301'
}
repositories {
    mavenCentral()
}