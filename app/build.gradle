plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'jacoco'
    id 'kotlin-android'
    id 'kotlin-parcelize'
}

apply from: 'signingCredentials.gradle'
apply from: "$project.rootDir/tools/jacoco.gradle"

android {
    namespace "ru.babaetskv.passionwoman.app"

    defaultConfig {
        applicationId "ru.babaetskv.passionwoman.app"
        minSdk 21
        compileSdk 34
        targetSdk 34
        versionCode 3
        versionName "0.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        demo {
            storeFile file(signingCredentials.filepath)
            keyAlias signingCredentials.alias
            keyPassword signingCredentials.keyPass
            storePassword signingCredentials.storePass
        }
    }

    flavorDimensions = ["version"]
    productFlavors {
        demo {
            dimension "version"
            applicationId "ru.babaetskv.passionwoman.demo.app"
            versionNameSuffix "-demo"
        }
        full {
            dimension "version"
            versionNameSuffix ""
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            productFlavors.demo.signingConfig signingConfigs.demo
            // TODO: add config for the full flavor
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "$applicationName${variant.productFlavors[0].versionNameSuffix}-${defaultConfig.versionName}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    implementation project(":data")
    implementation project(":domain")

    // Common
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation "android.arch.lifecycle:extensions:1.1.1"

    // ConstraintLayout
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Navigation
    implementation "com.github.terrakok:cicerone:7.0"

    // DI
    implementation "io.insert-koin:koin-android:3.1.5"

    // Glide
    def glide_version = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    // ViewBinding
    implementation "com.github.yogacp:android-viewbinding:1.0.3"

    // Shimmer
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    // Circle indicator
    implementation "me.relex:circleindicator:2.1.6"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:28.0.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-dynamic-links-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    // Country code picker
    implementation 'com.hbb20:ccp:2.5.0'

    // Pin entry view
    implementation 'me.philio:pinentryview:1.0.6'

    // Image picker
    implementation 'com.github.dhaval2404:imagepicker:2.1'

    // AdapterDelegates
    def adapter_delegates_version = "4.3.0"
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl:$adapter_delegates_version"
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl-viewbinding:$adapter_delegates_version"

    // Flexbox
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:2.19.1'

    // SplashScreen
    implementation 'androidx.core:core-splashscreen:1.0.1'

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:3.2.1"

    // WorkManager
    def work_version = "2.7.1"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Testing
    testImplementation project(path: ":domain", configuration: "testDependencies")
    androidTestImplementation project(path: ":domain", configuration: "androidTestDependencies")
}
repositories {
    mavenCentral()
}